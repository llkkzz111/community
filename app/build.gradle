apply plugin: 'com.android.application'
apply plugin: 'packer'

Properties props = new Properties()
props.load(new FileInputStream(file("signing.properties")))

Properties prop = new Properties()
def propFile = file('server_url.properties')
def betaUrl, releaseUrl, betaApiUrl, releaseApiUrl, UMAppID, devSocketHost, releaseSocketHost

if (propFile.canRead()) {
    prop.load(new FileInputStream(propFile))
    if (props != null) {
        betaUrl = prop['betaUrl']
        releaseUrl = prop['releaseUrl']
        betaApiUrl = prop['betaApiUrl']
        releaseApiUrl = prop['releaseApiUrl']
        UMAppID = prop['UMAppID']
        devSocketHost = prop['devSocketHost']
        releaseSocketHost = prop['releaseSocketHost']
    }
}

android {

    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.choudao.equity"
        minSdkVersion 16
        targetSdkVersion 22
        versionCode 12
        versionName "2.3.0"
        multiDexEnabled true

        ndk {
            abiFilters 'armeabi', 'armeabi-v7a', 'armeabi-v8a', 'x86', 'x86_64', 'mips', 'mips64'
        }
        manifestPlaceholders = [
                JPUSH_PKGNAME: "com.choudao.equity",
                JPUSH_APPKEY : "63164160401a23867868a07e", //JPush上注册的包名对应的appkey.
                JPUSH_CHANNEL: "choudao", //暂时填写默认值即可.
        ]
    }
    signingConfigs {
        release {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
            v2SigningEnabled false
        }
        debug {
            storeFile file('debug.keystore')
            v2SigningEnabled false
        }
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            versionNameSuffix "_debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            signingConfig signingConfigs.release
            minifyEnabled true
            zipAlignEnabled true
//            移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //关闭lint检查
    lintOptions {
        abortOnError false
        htmlReport true
    }
    packagingOptions {
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
    dexOptions {
//        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
        beta {
            buildConfigField 'String', 'BASE_URL', betaUrl
            buildConfigField 'String', 'BASE_API_URL', betaApiUrl
            buildConfigField 'String', 'SOCKET_IP', devSocketHost
            buildConfigField 'int', 'SOCKET_PORT', "39999"
        }
        choudao {
            buildConfigField 'String', 'BASE_URL', releaseUrl
            buildConfigField 'String', 'BASE_API_URL', releaseApiUrl
            buildConfigField 'String', 'SOCKET_IP', releaseSocketHost
            buildConfigField 'int', 'SOCKET_PORT', "19123"
        }
    }
    productFlavors.all {
        flavor ->
            flavor.manifestPlaceholders = [CHANNEL_NAME: name]
            buildConfigField 'String', 'UMENG_APPID', UMAppID
    }
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':imsdk')
    testCompile 'junit:junit:4.12'
    compile 'com.qiniu:qiniu-android-sdk:7.3.0'
    compile 'com.umeng.analytics:umeng-analytics:5.4.1'
    compile 'com.tonicartos:superslim:0.4.13'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    //glide
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //ptr-load-more
    compile 'in.srain.cube:ptr-load-more:1.0.5'
    //guava
    compile 'com.google.guava:guava:19.0'
    /**
     *  http://www.open-open.com/lib/view/open1453375899105.html
     */
    compile 'me.codeboy.android:align-text-view:2.3.0'
    //build
    compile 'com.mcxiaoke.gradle:packer-helper:1.0.7'
    //butterknife
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    //jpush
    compile 'cn.jiguang.sdk:jpush:3.0.1'
    compile 'cn.jiguang.sdk:jcore:1.1.0'
    //recycleview 高级应用
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.8.0'
    compile 'com.github.JakeWharton:ViewPagerIndicator:2.4.1'

    compile 'com.google.dagger:dagger:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
}
